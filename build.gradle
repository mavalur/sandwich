buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.moowork.gradle:gradle-gulp-plugin:0.11'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'com.moowork.gulp'

jar {
    baseName = 'hackathon'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

configurations {
    providedRuntime
    querydslapt
    compile.exclude module: "spring-boot-starter-tomcat"
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('javax.inject:javax.inject:1')
    compile('com.twitter:twitter-text:1.6.1')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-jetty:1.3.1.RELEASE")
    compile('org.springframework.boot:spring-boot-starter-websocket')
    compile('org.projectlombok:lombok:1.16.6')
    compile('com.google.guava:guava:18.0')
    compile('joda-time:joda-time:2.3')
    compile('com.mysema.querydsl:querydsl-mongodb:3.7.0')

    compile('org.springframework.data:spring-data-mongodb:1.8.2.RELEASE')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    querydslapt "com.mysema.querydsl:querydsl-apt:3.7.0"
}



dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.M3"
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "org.springframework.data.mongodb.repository.support.MongoAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {

    module {
        sourceDirs += file('src/main/generated')
    }

}

gulp {
    // Set the directory where gulpfile.js should be found
    workDir = file("static")

    // Whether colors should output on the terminal
    colors = true

    // Whether output from Gulp should be buffered - useful when running tasks in parallel
    bufferOutput = false
}

node {
    // Version of node to use.
    version = '0.10.22'

    // Enabled the automatic download. False is the default (for now).
    download = true
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

// makes sure on each build that gulp is installed
//gulp_build.dependsOn 'installGulp'

// processes your package.json before running gulp build
//gulp_build.dependsOn 'npmInstall'

// runs "gulp build" as part of your gradle build
//build.dependsOn gulp_build

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
